buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


version = "1.0"
group = "com.tajmoti.tubediscs" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "tubediscs"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2838"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20171003"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
    mavenCentral()
    maven { url = "http://dvs1.progwml6.com/files/maven" }
    maven { url = "http://tehnut.info/maven" }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile 'ws.schild:jave-core:2.5.0'
    compile 'ws.schild:jave-nativebin-win64:2.5.0'
    compile 'com.github.axet:vget:1.2.6'
    compile 'com.github.sapher:youtubedl-java:1.1'
}

shadowJar {
    // Only shadow fluent-hc
    dependencies {
        include(dependency('ws.schild:jave-core:2.5.0'))
        include(dependency('ws.schild:jave-nativebin-win64:2.5.0'))
        include(dependency('com.github.axet:vget:1.2.6'))
        include(dependency('com.github.sapher:youtubedl-java:1.1'))
    }

    // Relocate fluent-hc to prevent conflicts with other mods that include it
    relocate 'ws.schild', 'com.tajmoti.repack.ws.schild'
    relocate 'com.github.axet', 'com.tajmoti.repack.com.github.axet'
    relocate 'com.sapher', 'com.tajmoti.repack.com.sapher'

    classifier '' // Replace the default JAR
}

reobf {
    shadowJar {}
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

artifacts {
    archives shadowJar
}